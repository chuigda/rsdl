WHITESPACE = _{ (" " | "\t" | NEWLINE) }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

raw_string = @{ ("\\\"" | "\\t" | "\\n" | "\\r" | (!"\"" ~ ANY))* }

string = { "\"" ~ raw_string ~ "\"" }

mapping = { identifier ~ "=>" ~ string }

mapping_seq = { (mapping ~ ",")* ~ mapping }

list_type = { (
  "[" ~ rsdl_type ~ "]" |
  "list" ~ "<" ~ rsdl_type ~ ">"
) }

record_type = { "record" ~ "<" ~ "str" ~ "," ~ rsdl_type ~ ">" }

native_type = { "native(" ~ mapping_seq ~ ")" }

rsdl_type = {
  native_type |
  list_type |
  record_type |
  identifier
}

optional_mark = { "?"? }

field = { attr* ~ identifier ~ optional_mark ~ ":" ~ rsdl_type }

field_list = { (field ~ ",")* ~ field }

type_ctor = { identifier ~ "(" ~ field_list ~ ")" }

sum_type_variant = { attr* ~ (type_ctor | identifier) }

variant_list = { (sum_type_variant ~ "|")* ~ sum_type_variant }

sum_type = { identifier ~ ":" ~ variant_list }

type_alias = { identifier ~ "=" ~ rsdl_type }

call_attr = { identifier ~ "(" ~ attr_item_list ~ ")" }

assign_attr = { identifier ~ "=" ~ (call_attr | identifier | string) }

attr_item = { call_attr | assign_attr | identifier | string }

attr_item_list = { (attr_item ~ ",")* ~ attr_item }

attr = { "[" ~ attr_item ~ "]" }

type_def = { attr* ~ (type_ctor | sum_type | type_alias) }

global_attr = { "[" ~ "[" ~ attr_item ~ "]" ~ "]" }

rsdl_program = { SOI ~ (global_attr | type_def)* ~ EOI }
