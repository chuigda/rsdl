WHITESPACE = _{ (" " | "\t" | NEWLINE) }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

scoped_identifier = { (identifier ~ ".")* ~ identifier }

string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

mapping = { identifier ~ "=>" ~ (scoped_identifier | string) }

mapping_seq = {
  (mapping ~ ",")* ~ mapping
}

list_type = { "[" ~ rsdl_type ~ "]" }
record_type = { "{" ~ "str" ~ "=>" ~ rsdl_type ~ "}" }
native_type = { "native(" ~ mapping_seq ~ ")" }

rsdl_type = {
  "int" |
  "float" |
  "str" |
  "bool" |
  native_type |
  identifier |
  list_type |
  record_type
}

field = { attr* ~ identifier ~ ":" ~ rsdl_type }

field_list = { (field ~ ",")* ~ field }

type_ctor = { identifier ~ "(" ~ field_list ~ ")" }

sum_type_variant = { type_ctor | identifier }

variant_list = { (sum_type_variant ~ "|")* ~ sum_type_variant }

sum_type = { identifier ~ ":" ~ variant_list }

type_alias = { identifier ~ "=" ~ rsdl_type }

comp_attr = { identifier ~ "(" ~ attr_item_list ~ ")" }

assign_attr = { identifier ~ "=" ~ (identifier | scoped_identifier | string) }

attr_item = { comp_attr | assign_attr | identifier | string }

attr_item_list = { (attr_item ~ ",")* ~ attr_item }

attr = { "[" ~ attr_item ~ "]" }

type_decl = { attr* ~ (type_ctor | sum_type | type_alias) }

rsdl_program = { SOI ~ type_decl* ~ EOI }
